import com.github.jengelman.gradle.plugins.shadow.transformers.ResourceTransformer
import com.github.jengelman.gradle.plugins.shadow.transformers.TransformerContext
import org.apache.tools.zip.ZipEntry
import org.apache.tools.zip.ZipOutputStream
import java.nio.charset.StandardCharsets
import java.nio.file.Files
import java.nio.file.StandardOpenOption

plugins {
    alias(libs.plugins.kotlin.jvm)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.shadow)
    alias(libs.plugins.canvas.weaver.userdev)
    alias(libs.plugins.resource.factory.paper)
}

group = "me.yin.simpleworld"
version = "1.0.0-SNAPSHOT"

kotlin {
    jvmToolchain(25)
}

repositories {
    mavenLocal()
    mavenCentral()
    maven("https://maven.canvasmc.io/releases")
}

dependencies {
    paperweight.canvasDevBundle("26.1.2.build.+")

    implementation(libs.kotlinx.serialization.json)
    implementation(libs.kotlinx.coroutines.core)
}

val minecraftPluginName = "SimpleWorld"
val minecraftPluginApiVersion = "1.21.11"
val minecraftPluginVersion = project.version.toString()
val minecraftPluginAuthors = listOf("尹")
val minecraftPluginPrefix = "简单世界"
val minecraftPluginGroup = project.group.toString()
val minecraftPluginMain = "$minecraftPluginGroup.$minecraftPluginName"
val minecraftPluginLoader = "$minecraftPluginGroup.${minecraftPluginName}Loader"
val minecraftPluginJarName = "$minecraftPluginName-${project.name}"
val minecraftPluginJarFileName = "$minecraftPluginJarName.jar"
val minecraftPluginShadowJarFileName = "$minecraftPluginJarName-shadow.jar"

paperPluginYaml {
    name = minecraftPluginName
    apiVersion = minecraftPluginApiVersion
    version = minecraftPluginVersion
    main = minecraftPluginMain
    authors = minecraftPluginAuthors
    prefix = minecraftPluginPrefix
    loader = minecraftPluginLoader
    foliaSupported = true
}

val generatePaperLibraries by tasks.registering {
    val outputFile = layout.buildDirectory.file("generated/paper/libraries.text")
    outputs.file(outputFile)
    inputs.files(configurations.runtimeClasspath)

    doLast {
        val libraries = configurations.runtimeClasspath.get()
            .resolvedConfiguration
            .firstLevelModuleDependencies

        val outputFilePath = outputFile.get().asFile.toPath()
        Files.createDirectories(outputFilePath.parent)

        Files.newBufferedWriter(outputFilePath, StandardCharsets.UTF_8, StandardOpenOption.CREATE, StandardOpenOption.TRUNCATE_EXISTING).use { writer ->
            libraries.forEach {
                writer.write("${it.moduleGroup}:${it.moduleName}:${it.moduleVersion}")
                writer.newLine()
            }
        }
    }
}

tasks.processResources {
    from(generatePaperLibraries)
}

tasks.jar {
    archiveFileName.set(minecraftPluginJarFileName)
}

class LibrariesTextLibrariesRemover : ResourceTransformer {
    private var transformed = false

    override fun canTransformResource(element: FileTreeElement): Boolean {
        val isTarget = element.relativePath.pathString == "libraries.text"
        if (isTarget) {
            transformed = true
        }
        return isTarget
    }

    override fun transform(context: TransformerContext) {
    }

    override fun hasTransformedResource(): Boolean {
        return transformed
    }

    override fun modifyOutputStream(os: ZipOutputStream, preserveFileTimestamps: Boolean) {
        val entry = ZipEntry("libraries.text")
        entry.time = System.currentTimeMillis()
        os.putNextEntry(entry)
        os.write(ByteArray(0))
        os.closeEntry()
    }
}

tasks.shadowJar {
    mergeServiceFiles()
    archiveFileName.set(minecraftPluginShadowJarFileName)
    transform(LibrariesTextLibrariesRemover())
}
